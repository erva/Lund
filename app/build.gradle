apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def majorVersion = 1.4

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.erva.lund"
        minSdkVersion 20
        targetSdkVersion 28
        versionName "${majorVersion}"
        versionCode getCommitCount()
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    androidExtensions {
        experimental = true
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File("./", "lund_${baseName}-${majorVersion}.${getCommitCount()}.apk")
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation ('com.github.erva.CellAdapter:celladapter-kotlin:3.0.0') {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'androidx.recyclerview', module: 'recyclerview'
    }
}

static def getCommitCount() {
    return "git rev-list HEAD --count".execute().text.trim().toInteger()
}